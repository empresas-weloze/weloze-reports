
-- ALTER VIEW BiView.CotacoesVenda_OMT AS

SELECT 
	
	QT.QUOTATIONID								AS "Cotação - Número",
	QT.CREATEDDATETIME							AS "Cotação - Data de Criação",
	CONCAT(OT.OPERATIONTYPEID, ' - ', OT.NAME)	AS "Tipo de Operação",

	STATUSCOTACAOVENDA.SalesQuotationStatus		AS "Status",
	SL.SALESID									AS "Ordem de Venda",

	QT.CREATEDDATETIME 			AS "Dt. Criação",
	DTCRIACAO.SEMANA_ANO		AS "Dt. Criação (Semana/Ano)",
	DTCRIACAO.MES_ANO			AS "Dt. Criação (Mês/Ano)",

	QT.SHIPPINGDATEREQUESTED	AS "Dt. Entrega",
	DTENTREGA.SEMANA_ANO		AS "Dt. Entrega (Semana/Ano)",
	DTENTREGA.MES_ANO			AS "Dt. Entrega (Mês/Ano)",

	-- Se não tem cliente, retorna o cliente potencial. NULLIF pois o sistema não salva NULL, mas uma string vazia ''
	CONCAT(COALESCE(NULLIF(QT.CUSTACCOUNT, ''), QT.BUSRELACCOUNT), ' - ', CL.NAME) AS "Cliente",
	
	CASE WHEN QT.CUSTACCOUNT = '' THEN 'Cliente Potencial' ELSE 'Cliente' END AS "Tipo de Cliente",

	CONCAT(PN.FIRSTNAME, ' ', PN.MIDDLENAME, ' ', PN.LASTNAME) AS "Tomador de Vendas",


	CONCAT(IT.ITEMID, ' - ', PT.NAME)	AS "Item",
	CFOP.CFOPID							AS "CFOP",

--	QL.SALESQTY		AS "Quantidade",  
--	QL.LINEAMOUNT	AS "Valor",

	D.CONFIGID			AS "Dimensão - Configuração",
	D.INVENTSIZEID		AS "Dimensão - Tensão",
	D.INVENTCOLORID		AS "Dimensão - Cor",
	D.INVENTSITEID		AS "Dimensão - Site",
	D.INVENTLOCATIONID	AS "Dimensão - Depósito",
	D.WMSLOCATIONID		AS "Localização",
	D.WMSPALLETID		AS "ID do palete",
	D.INVENTBATCHID		AS "Nº do lote",
	D.INVENTSERIALID	AS "Nº de série",

	(QL.LINEAMOUNT + COALESCE(IPI.VALOR, 0)) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1.0 ELSE CAMBIOATUAL.TAXA END) AS "Valor Bruto",
	(QL.LINEAMOUNT) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1 ELSE CAMBIOATUAL.TAXA END) AS "Valor Bruto s/ IPI",

	COALESCE(IPI.VALOR, 0) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1.0 ELSE CAMBIOATUAL.TAXA END) AS "Valor IPI",
	COALESCE(PIS.VALOR, 0) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1.0 ELSE CAMBIOATUAL.TAXA END) AS "Valor PIS",
	COALESCE(ICMS.VALOR, 0) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1.0 ELSE CAMBIOATUAL.TAXA END) AS "Valor ICMS",
	COALESCE(COFINS.VALOR, 0) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1.0 ELSE CAMBIOATUAL.TAXA END) AS	"Valor COFINS",
	COALESCE(ISSQN.VALOR, 0) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1.0 ELSE CAMBIOATUAL.TAXA END) AS "Valor ISSQN",

	(QL.LINEAMOUNT - COALESCE(PIS.VALOR, 0) - COALESCE(COFINS.VALOR, 0) - COALESCE(ICMS.VALOR, 0) - COALESCE(ISSQN.VALOR, 0)) * (CASE WHEN QL.CURRENCYCODE = 'BRL' THEN 1.0 ELSE CAMBIOATUAL.TAXA END) AS "Valor Líquido"

FROM SALESQUOTATIONTABLE QT
	JOIN SALESQUOTATIONLINE QL ON QL.QUOTATIONID = QT.QUOTATIONID
	LEFT JOIN CUSTTABLE CT ON CT.ACCOUNTNUM = QT.CUSTACCOUNT	
	LEFT JOIN SMMBUSRELTABLE RL ON RL.BUSRELACCOUNT = QT.BUSRELACCOUNT
	LEFT JOIN HCMWORKER HW ON HW.RECID = QT.WORKERSALESTAKER
	LEFT JOIN DIRPERSONNAME PN ON PN.PERSON= HW.PERSON
	LEFT JOIN SALESPURCHOPERATIONTYPE_BR OT ON OT.RECID = QT.SALESPURCHOPERATIONTYPE_BR 
	LEFT JOIN CFOPTABLE_BR CFOP ON CFOP.RECID = QL.CFOPTABLE_BR
	JOIN INVENTTABLE IT ON IT.ITEMID = QL.ITEMID AND IT.DATAAREAID = QL.DATAAREAID
	JOIN ECORESPRODUCT P ON P.RECID = IT.PRODUCT
	JOIN ECORESPRODUCTTRANSLATION PT ON PT.PRODUCT = P.RECID AND PT.LANGUAGEID = 'PT-BR'
	JOIN INVENTDIM D ON D.INVENTDIMID = QL.INVENTDIMID
	CROSS APPLY BiUtil.StatusCotacaoVenda (QT.QUOTATIONSTATUS) STATUSCOTACAOVENDA
	LEFT JOIN CUSTQUOTATIONCONFIRMSALESLINK SL ON SL.ORIGQUOTATIONID = QT.QUOTATIONID
	JOIN DIRPARTYTABLE CL ON CL.RECID = CT.PARTY OR CL.RECID = RL.PARTY

	OUTER APPLY BiUtil.ImpostoPorGrupos (QT.DATAAREAID, QL.TAXGROUP, QL.TAXITEMGROUP, 2, QL.LINEAMOUNT) AS ICMS
	OUTER APPLY BiUtil.ImpostoPorGrupos (QT.DATAAREAID, QL.TAXGROUP, QL.TAXITEMGROUP, 8, QL.LINEAMOUNT) AS IPI
	OUTER APPLY BiUtil.ImpostoPorGrupos (QT.DATAAREAID, QL.TAXGROUP, QL.TAXITEMGROUP, 1, QL.LINEAMOUNT) AS PIS
	OUTER APPLY BiUtil.ImpostoPorGrupos (QT.DATAAREAID, QL.TAXGROUP, QL.TAXITEMGROUP, 3, QL.LINEAMOUNT) AS COFINS
	OUTER APPLY BiUtil.ImpostoPorGrupos (QT.DATAAREAID, QL.TAXGROUP, QL.TAXITEMGROUP, 4, QL.LINEAMOUNT) AS ISSQN

	CROSS APPLY BiUtil.DataFiltro(QT.CREATEDDATETIME) DTCRIACAO
	CROSS APPLY BiUtil.DataFiltro(QT.SHIPPINGDATEREQUESTED) DTENTREGA
	
	OUTER APPLY BiUtil.TaxaCambioAtual	(QL.CURRENCYCODE) AS CAMBIOATUAL

WHERE QT.DATAAREAID = 'OMT'
	AND QT.QUOTATIONSTATUS <> 4 -- Exclui as canceladas	
	AND (( 
		OT.CREATEFINANCIALTRANS = 1 -- Movimenta financeiro
		AND OT.OPERATIONTYPEID NOT IN (
			'S5556/6556', 'S5201/6201', 'S5410', 'S5412/6412', 'S5413/6413', 'S5553/6553', 'S5556/6556', 'S5201/6201', 'S413', 'S201', 'S5206', 'DC5201', 'S5413', -- devolução de compra
			'S922', 'S5922', 'S6922', 'S5922/6922', -- exclui simples faturamento
			'S5551/6551', -- venda de ativo imobilizado
			'S5206/6206', 'S5206' -- anulação de frete
				)
			)
			OR (OT.OPERATIONTYPEID = 'S5902/5124' AND CFOP.CFOPID = '5.124') -- operação especial Master
			OR (OT.OPERATIONTYPEID = 'T5902/5124' AND CFOP.CFOPID = '5.124') -- operação especial Master
			OR OT.OPERATIONTYPEID IN ('S116', 'S5116', 'S6116', 'S6118', 'S5116/6116', 'S5117/6117') -- inclui simples remessa
	)
	
