
-- ALTER VIEW [BiView].[ReceitaLiquida_Resumo_Weloze]  AS

SELECT 

	DATAFILTRO.MESANO AS "Data - Mês/Ano",
--	DATEPART(ISO_WEEK, X.FISCALDOCUMENTDATE) AS "Data - Nº da Semana",

	CONCAT(X.FISCALDOCUMENTACCOUNTNUM, ' - ', X.CLIENTE) AS "Cliente - Nome",

	SUM(X.FATOR * (X.VALORLINHA + X.IPI + X.ENCARGOS)) AS "Valor Fat. Bruto",
	SUM(X.FATOR * (X.VALORLINHA + X.ENCARGOS - X.PIS - X.ICMS - X.COFINS - X.ISSQN)) AS "Valor Rec. Líquida"

 FROM 
(

	-- Saídas de faturamento (excluindo devoluções de compra e simples faturamento)

	SELECT 

		NF.FISCALDOCUMENTACCOUNTNUM, 
		PT.NAME AS CLIENTE, 
		LNF.LINEAMOUNT VALORLINHA, 
		IIPI.VALOR AS IPI,
		IPIS.VALOR AS PIS,
		IICMS.VALOR AS ICMS,
		ICOFINS.VALOR AS COFINS,
		IISSQN.VALOR AS ISSQN,
		ENCARGO.ENCARGO AS ENCARGOS,
		1 AS FATOR,
		NF.FISCALDOCUMENTDATE

	FROM FISCALDOCUMENT_BR NF
		JOIN FISCALDOCUMENTLINE_BR LNF ON LNF.FISCALDOCUMENT = NF.RECID
		JOIN CUSTINVOICEJOUR I ON I.RECID = NF.REFRECID AND NF.REFTABLEID = 62
		LEFT JOIN SALESTABLE S ON S.SALESID = I.SALESID
		LEFT JOIN SALESTABLE_BR SBR ON SBR.SALESTABLE = S.RECID
		LEFT JOIN SALESPURCHOPERATIONTYPE_BR OT ON OT.RECID = SBR.SALESPURCHOPERATIONTYPE_BR
		JOIN CUSTTABLE C ON C.ACCOUNTNUM = NF.FISCALDOCUMENTACCOUNTNUM
		JOIN DIRPARTYTABLE PT ON PT.RECID = C.PARTY
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 8) AS IIPI
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 1) AS IPIS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 2) AS IICMS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 3) AS ICOFINS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 4) AS IISSQN
		CROSS APPLY WELO.EncargoLinha (LNF.RECID) AS ENCARGO
	WHERE 1=1 
		AND LNF.DATAAREAID = 'WELO'
		AND NF.STATUS = 1 -- Status da Nota Fiscal = Aprovado
		AND NF.DIRECTION = 2 -- Direção Saída
		AND (( 
			OT.CREATEFINANCIALTRANS = 1 -- Movimenta financeiro
			AND OT.OPERATIONTYPEID NOT IN (
				'S5556/6556', 'S5201/6201', 'S5410', 'S5412/6412', 'S5413/6413', 'S5553/6553', 'S5556/6556', 'S5201/6201', 'S413', 'S201', 'S5206', 'DC5201', -- devolução de compra
				'S922', 'S5922', 'S6922', 'S5922/6922', -- exclui simples faturamento
				'S5551/6551', -- venda de ativo imobilizado
				'S5206/6206', 'S5206' -- anulação de frete
					)
				)
				OR (OT.OPERATIONTYPEID = 'S5902/5124' AND LNF.CFOP = '5.124') -- operação especial Master
				OR OT.OPERATIONTYPEID IN ('S116', 'S5116', 'S6116', 'S6118', 'S5116/6116') -- inclui simples remessa
		)


	UNION

	-- Ordens de Devolução

	SELECT 
	
		NF.FISCALDOCUMENTACCOUNTNUM, 
		PT.NAME AS CLIENTE, 
		LNF.LINEAMOUNT VALORLINHA, 
		IIPI.VALOR AS IPI,
		IPIS.VALOR AS PIS,
		IICMS.VALOR AS ICMS,
		ICOFINS.VALOR AS COFINS,
		IISSQN.VALOR AS ISSQN,
		ENCARGO.ENCARGO AS ENCARGOS,
		-1 AS FATOR,
		NF.FISCALDOCUMENTDATE

	FROM FISCALDOCUMENT_BR NF
		JOIN FISCALDOCUMENTLINE_BR LNF ON LNF.FISCALDOCUMENT = NF.RECID
		JOIN CUSTINVOICEJOUR I ON I.RECID = NF.REFRECID AND NF.REFTABLEID = 62
		LEFT JOIN SALESTABLE S ON S.SALESID = I.SALESID
		LEFT JOIN SALESTABLE_BR SBR ON SBR.SALESTABLE = S.RECID
		LEFT JOIN SALESPURCHOPERATIONTYPE_BR OT ON OT.RECID = SBR.SALESPURCHOPERATIONTYPE_BR
		JOIN CUSTTABLE C ON C.ACCOUNTNUM = NF.FISCALDOCUMENTACCOUNTNUM
		JOIN DIRPARTYTABLE PT ON PT.RECID = C.PARTY
		JOIN CFOPTABLE_BR CF ON CF.CFOPID = LNF.CFOP AND CF.DATAAREAID = LNF.DATAAREAID
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 8) AS IIPI
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 1) AS IPIS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 2) AS IICMS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 3) AS ICOFINS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 4) AS IISSQN
		CROSS APPLY WELO.EncargoLinha (LNF.RECID) AS ENCARGO
	WHERE 1=1 
		AND LNF.DATAAREAID = 'WELO'
		AND NF.STATUS = 1 
		AND NF.DIRECTION = 1
		AND (
			OT.OPERATIONTYPEID IN ('F116', 'S101/102', 'S107/108', 'S118', 'S501', 'S5101', 'S5101 001', 'S5101 V.SU', 
				'S5102', 'S5116', 'S5116/6116', 'S5122/6122', 'S5124/5125', 'S5124/S5125', 'S5501/6501', 'S5118', 'S5118/6118', 
				'S5551/6551', 'S6101', 'S6101 001', 'S6116', 'S6118', 'S6122', 'S6124/6125', 'S7101/7127', 'S933', 
				'SFERRAM.RA', 'SFERRAMENT', 'SSUCATAS')
			OR (OT.OPERATIONTYPEID = 'S5902/5124' AND LNF.CFOP = '5.124') -- operação especial Master
		)
		AND OT.CREATEFINANCIALTRANS = 1 


	UNION

	-- Devoluções por Ordem de Compra

	SELECT 
	
		CT.ACCOUNTNUM,
		PT.NAME AS CLIENTE, 
		LNF.LINEAMOUNT VALORLINHA, 
		IIPI.VALOR AS IPI,
		IPIS.VALOR AS PIS,
		IICMS.VALOR AS ICMS,
		ICOFINS.VALOR AS COFINS,
		IISSQN.VALOR AS ISSQN,
		ENCARGO.ENCARGO AS ENCARGOS,
		-1 AS FATOR,
		NF.FISCALDOCUMENTDATE

	FROM FISCALDOCUMENT_BR NF
		JOIN FISCALDOCUMENTLINE_BR LNF ON LNF.FISCALDOCUMENT = NF.RECID
		JOIN VENDINVOICEJOUR I ON I.RECID = NF.REFRECID AND NF.REFTABLEID = 491
		LEFT JOIN PURCHTABLE P ON P.PURCHID = I.PURCHID
		LEFT JOIN PURCHTABLE_BR PBR ON PBR.PURCHTABLE = P.RECID
		LEFT JOIN SALESPURCHOPERATIONTYPE_BR OT ON OT.RECID = PBR.SALESPURCHOPERATIONTYPE_BR
		JOIN VENDTABLE V ON V.ACCOUNTNUM = NF.FISCALDOCUMENTACCOUNTNUM
		JOIN DIRPARTYTABLE PT ON PT.RECID = V.PARTY
		JOIN CUSTTABLE CT ON CT.CNPJCPFNUM_BR = V.CNPJCPFNUM_BR AND CT.DATAAREAID = V.DATAAREAID
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 8) AS IIPI
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 1) AS IPIS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 2) AS IICMS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 3) AS ICOFINS
		CROSS APPLY WELO.ImpostoLinha (LNF.RECID, 4) AS IISSQN
		CROSS APPLY WELO.EncargoLinha (LNF.RECID) AS ENCARGO
	WHERE 1=1 
		AND NF.STATUS = 1 
		AND NF.DIRECTION = 1
		AND LNF.DATAAREAID = 'WELO'
		AND OT.OPERATIONTYPEID IN ('E1201/2201', 'DV1201/2201', 'E201/202')
		AND OT.CREATEFINANCIALTRANS = 1 
) AS X

CROSS APPLY BI.DataFiltroAnoMes(X.FISCALDOCUMENTDATE) AS DATAFILTRO

GROUP BY DATAFILTRO.MESANO, X.FISCALDOCUMENTACCOUNTNUM, X.CLIENTE
